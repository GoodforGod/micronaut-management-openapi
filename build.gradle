plugins {
    id "jacoco"
    id "maven-publish"

    id "org.sonarqube" version "4.3.0.3225"
    id "com.diffplug.spotless" version "6.19.0"
    id "io.micronaut.library" version "4.0.2"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

group = groupId
var ver = System.getenv().getOrDefault("RELEASE_VERSION", artifactVersion)
version = ver.startsWith("v") ? ver.substring(1) : ver

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

micronaut {
    version("4.0.2")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
    }
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"

    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "io.swagger.core.v3:swagger-annotations"
    compileOnly "io.micronaut:micronaut-http-server"
    compileOnly "org.jetbrains:annotations:24.0.1"

    implementation "org.yaml:snakeyaml"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut.openapi:micronaut-openapi"

    testImplementation "io.micronaut:micronaut-jackson-databind"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.micronaut.test:micronaut-test-core"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testCompileOnly "io.swagger.core.v3:swagger-annotations"

    testRuntimeOnly "io.micronaut:micronaut-http-server-netty"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.2.10"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
}

test {
    failFast(false)
    useJUnitPlatform()
    testLogging {
        showStandardStreams(false)
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    environment([
            "SECURITY_ENABLED": "false",
    ])

    reports {
        html.required = false
        junitXml.required = false
    }
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21").configFile("${projectDir}/config/codestyle.xml")
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "goodforgod"
        property "sonar.projectKey", "GoodforGod_$artifactId"
    }
}

nexusPublishing {
    packageGroup = groupId
    repositories {
        sonatype {
            username = System.getenv("OSS_USERNAME")
            password = System.getenv("OSS_PASSWORD")
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Micronaut Management OpenAPI"
                url = "https://github.com/GoodforGod/$artifactId"
                description = "Micronaut cloud-friendly OpenAPI/Swagger-UI/Rapidoc management endpoints."

                license {
                    name = "Apache License 2.0"
                    url = "https://github.com/GoodforGod/$artifactId/blob/master/LICENSE"
                    distribution = "repo"
                }

                developer {
                    id = "GoodforGod"
                    name = "Anton Kurako"
                    email = "goodforgod.dev@gmail.com"
                    url = "https://github.com/GoodforGod"
                }

                scm {
                    connection = "scm:git:git://github.com/GoodforGod/${artifactId}.git"
                    developerConnection = "scm:git:ssh://GoodforGod/${artifactId}.git"
                    url = "https://github.com/GoodforGod/$artifactId/tree/master"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
        if (!version.endsWith("SNAPSHOT")) {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/GoodforGod/$artifactId"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

if (project.hasProperty("signingKey")) {
    apply plugin: "signing"
    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}